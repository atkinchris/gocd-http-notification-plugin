/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'com.chrisatkin.gocd-plugins'
version = '0.1-SNAPSHOT'

// these values that go into plugin.xml
project.ext.pluginDesc = [
    id         : 'com.chrisatkin.gocd-http-notification-plugin',
    version    : project.version,
    goCdVersion: '16.2.1',
    name       : 'HTTP Notification Plugin',
    description: 'Notification plugin to POST status changes over HTTP',
    vendorName : 'Chris Atkin',
    vendorUrl  : 'https://github.com/atkinchris/gocd-http-notification-plugin'
]

// Force 1.7 compatibility to ensure that it works with older JVMs
sourceCompatibility = 1.7
targetCompatibility = 1.7

// In this section you declare where to find the dependencies of your project
repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
}

dependencies {
  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
  compile 'org.apache.httpcomponents:httpcore:4.4.4'
  compile 'org.apache.httpcomponents:httpclient:4.5.2'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
  compile group: 'com.google.guava', name: 'guava', version: '19.0'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
}

processResources {
  from("src/main/resource-templates") {
    filesMatching('plugin.xml') {
      expand project.pluginDesc
    }

    filesMatching('plugin.properties') {
      expand project.pluginDesc
    }
  }
}

jar {
  from(configurations.compile) {
    into "lib/"
  }
}
